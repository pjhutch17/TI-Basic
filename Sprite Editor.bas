



       100 REM SPRITE editor/manager
       110 CALL CLEAR
       120 OPTION BASE 1
       130 DIM G$(16),F$(20,2)
       140 CALL SCREEN(11):: CALL COLOR(12,14,1):: CALL HCHAR(1,1,126,768)
       150 DISPLAY AT(7,4)SIZE(21):"SPRITE EDITOR/MANAGER"
       160 DISPLAY AT(10,7)SIZE(15):"'BY PJH,14/10/84"
       170 DISPLAY AT(13,5)SIZE(18):"FOR 99/4A MAGAZINE"
       180 FOR A=1 TO 2000 :: NEXT A
       190 CALL CHAR(128,"FF818181818181FF",136,RPT$("F",16))
       200 CALL COLOR(13,5,1,14,9,1):: CALL CLEAR
       210 GOSUB 420
       220 FL=0
       230 DISPLAY AT(20,2)SIZE(8):"COMMAND:"
       240 ACCEPT AT(20,10)VALIDATE(UALPHA)SIZE(8):C$
       250 DISPLAY AT(22,1)SIZE(28):
       260 IF LEN(C$)=2 THEN GOSUB 500 :: GOTO 240
       270 C$=SEG$(C$,1,3)
       280 IF C$="HEL" THEN GOSUB 650 :: GOTO 240
       290 IF C$="COL" THEN GOSUB 720 :: GOTO 240
       300 IF C$="CLE" THEN GOSUB 420 :: GOTO 240
       310 IF C$="STO" THEN GOSUB 810 :: GOTO 240
       320 IF C$="INV" THEN GOSUB 1000 :: GOTO 240
       330 IF C$="DIS" THEN GOSUB 1100 :: GOTO 240
       340 IF C$="DRE" THEN GOSUB 1380 :: GOTO 240
       350 IF C$="ARE" THEN GOSUB 1480 :: GOTO 240
       360 IF C$="SAV" THEN GOSUB 1480 :: GOTO 240
       370 IF C$="REA" THEN GOSUB 1670 :: GOTO 240
       380 IF C$="REP" THEN GOSUB 1790 :: GOTO 240
       390 IF C$="EXI" THEN STOP
       400 DISPLAY AT(22,3):"NO SUCH COMMAND"
       410 GOTO 240
       420 FOR A=1 TO 16
       430 G$(A)=RPT$("0",16)
       440 CALL HCHAR(A+2,4,128,16)
       450 NEXT A
       460 FOR A=3 TO 18
       470 CALL HCHAR(A,3,62+A):: CALL HCHAR(2,A+1,62+A)
       480 NEXT A
       490 RETURN
       500 L$=SEG$(C$,1,1)
       510 IF NOT(L$>="A" AND L$<="P")THEN 630
       520 A=ASC(L$)-64
       530 L$=SEG$(C$,2,1)
       540 IF NOT(L$>="A" AND L$<="P")THEN 630
       550 B=ASC(L$)-64
       560 IF SEG$(G$(A),B,1)="1" THEN 600
       570 CALL HCHAR(A+2,B+3,136)
       580 G$(A)=SEG$(G$(A),1,B-1)&"1"&SEG$(G$(A),B+1,16-B)
       590 RETURN
       600 CALL HCHAR(A+2,B+3,128)
       610 G$(A)=SEG$(G$(A),1,B-1)&"0"&SEG$(G$(A),B+1,16-B)
       620 RETURN
       630 DISPLAY AT(22,2)SIZE(11):"BAD CO-ORDS"
       640 RETURN
       650 RESTORE
       660 FOR A=5 TO 16
       670 READ L$
       680 DISPLAY AT(A,20):L$
       690 NEXT A
       700 DATA HELP,COLOUR,CLEAR,STORE,INVERT,DISPLAY,DREFLECT,AREFLECT,SAV
       E,READ,REPLACE,EXIT
       710 RETURN
       720 DISPLAY AT(22,1)SIZE(13):"FORE` COLOUR?"
       730 ACCEPT AT(22,14)VALIDATE(DIGIT)SIZE(2):C
       740 IF C<1 OR C>16 THEN 730
       750 CALL COLOR(14,C,1)
       760 DISPLAY AT(22,1)SIZE(13):"BACK' COLOUR?"
       770 ACCEPT AT(22,14)VALIDATE(DIGIT)SIZE(2):C
       780 IF C<1 OR C>16 THEN 770
       790 CALL COLOR(13,C,1)
       800 RETURN
       810 IF FL+1>20 THEN DISPLAY AT(22,1)SIZE(21):"FILE FULL. USE REPLACE"
        :: RETURN
       820 FL=FL+1
       830 DISPLAY AT(22,3):"NAME?"


       840 ACCEPT AT(22,9)VALIDATE(UALPHA," ")SIZE(8):F$(FL,1)
       850 A=0
       860 A=A+1
       870 IF F$(A,1)=F$(FL,1)AND FL<>1 THEN 840
       880 IF A<FL-1 THEN 860
       890 GOSUB 920
       900 F$(FL,2)=P$
       910 RETURN
       920 P$="" :: FOR A=0 TO 1 :: FOR B=1 TO 16
       930 L$=SEG$(G$(B),A*8+1,8):: R,P=0
       940 FOR K=8 TO 1 STEP -1 :: R=R+2^P*VAL(SEG$(L$,K,1)):: P=P+1 :: NEXT
        K
       950 H$="0123456789ABCDEF"
       960 P$=P$&SEG$(H$,INT(R/16)+1,1)&SEG$(H$,R-INT(R/16)*16+1,1)
       970 NEXT B :: NEXT A
       980 RETURN
       990 REM
       1000 FOR A=1 TO 16
       1010 FOR B=1 TO 16
       1020 IF SEG$(G$(A),B,1)="1" THEN 1050
       1030 G$(A)=SEG$(G$(A),1,B-1)&"1"&SEG$(G$(A),B+1,16-B)
       1040 CALL HCHAR(A+2,B+3,136):: GOTO 1070
       1050 G$(A)=SEG$(G$(A),1,B-1)&"0"&SEG$(G$(A),B+1,16-B)
       1060 CALL HCHAR(A+2,B+3,128)
       1070 NEXT B
       1080 NEXT A
       1090 RETURN
       1100 IF FL=0 THEN DISPLAY AT(22,2)SIZE(10):"FILE EMPTY" :: RETURN
       1110 DISPLAY AT(22,5)SIZE(6):"NAME?"
       1120 ACCEPT AT(22,11)VALIDATE(UALPHA," ")SIZE(8):L$
       1130 A=0
       1140 A=A+1
       1150 IF L$=F$(A,1)THEN 1180
       1160 IF A<>FL THEN 1140
       1170 DISPLAY AT(22,2):L$;" NOT FOUND" :: RETURN
       1180 FOR B=1 TO 16
       1190 L$=SEG$(F$(A,2),B*2-1,2)&SEG$(F$(A,2),32+B*2-1,2):: R,P=0
       1200 FOR C=4 TO 1 STEP -1
       1210 R=R+16^P*(POS("0123456789ABCDEF",SEG$(L$,C,1),1)-1):: P=P+1
       1220 NEXT C
       1230 P$=""
       1240 P$=STR$(R-INT(R/2)*2)&P$ :: R=INT(R/2):: IF R<>0 THEN 1240
       1250 IF LEN(P$)<16 THEN P$="0"&P$ :: GOTO 1250
       1260 G$(B)=P$
       1270 NEXT B
       1280 FOR B=1 TO 16
       1290 FOR C=1 TO 16
       1300 IF SEG$(G$(B),C,1)="1" THEN CALL HCHAR(B+2,C+3,136)ELSE CALL HCH
       AR(B+2,C+3,128)
       1310 NEXT C
       1320 NEXT B
       1330 FOR B=1 TO 4
       1340 DISPLAY AT(22,3)SIZE(16):SEG$(F$(A,2),B*16-15,16)
       1350 CALL KEY(0,K,S):: IF S=0 THEN 1350
       1360 NEXT B
       1370 RETURN
       1380 FOR A=1 TO 8
       1390 FOR B=1 TO 16
       1400 IF SEG$(G$(A),B,1)="1" THEN 1430
       1410 G$(17-A)=SEG$(G$(17-A),1,B-1)&"0"&SEG$(G$(17-A),B+1,16-B)
       1420 CALL HCHAR(17-A+2,B+3,128):: GOTO 1450
       1430 G$(17-A)=SEG$(G$(17-A),1,B-1)&"1"&SEG$(G$(17-A),B+1,16-B)
       1440 CALL HCHAR(17-A+2,B+3,136)
       1450 NEXT B
       1460 NEXT A
       1470 RETURN
       1480 FOR A=1 TO 16
       1490 FOR B=1 TO 8
       1500 IF SEG$(G$(A),B,1)="1" THEN 1530
       1510 G$(A)=SEG$(G$(A),1,17-B-1)&"0"&SEG$(G$(A),17-B+1,16(17-B))
       1520 CALL HCHAR(A+2,17-B+3,128):: GOTO 1550
       1530 G$(A)=SEG$(G$(A),1,17-B-1)&"1"&SEG$(G$(A),17-B+1,16-(17-B))
       1540 CALL HCHAR(A+2,17-B+3,136)
       1550 NEXT B
       1560 NEXT A
       1570 RETURN
       1580 CALL CLEAR


       1590 OPEN #1:"CS1",INTERNAL,OUTPUT,VARIABLE 128
       1600 PRINT #1:FL
       1610 FOR A=1 TO FL :: PRINT #1:F$(A,1),F$(A,2):: NEXT A
       1620 CLOSE #1
       1630 CALL CLEAR
       1640 GOSUB 420
       1650 DISPLAY AT(20,2)SIZE(8):"COMMAND:"
       1660 RETURN
       1670 CALL CLEAR
       1680 OPEN #1:"CS1",INTERNAL,INPUT ,VARIABLE 128
       1690 INPUT #1:FL
       1700 FOR A=1 TO FL :: INPUT #1:F$(A,1),F$(A,2):: NEXT A
       1710 CLOSE #1
       1720 CALL CLEAR
       1730 GOSUB 420
       1740 DISPLAY AT(20,2)SIZE(8):"COMMAND:"
       1750 FOR A=1 TO FL
       1760 DISPLAY AT(A,20):F$(A,1)
       1770 NEXT A
       1780 RETURN
       1790 DISPLAY AT(22,1)SIZE(13):"NAME IN FILE?"
       1800 ACCEPT AT(22,14)VALIDATE(UALPHA," ")SIZE(8):L$
       1810 A=0
       1820 A=A+1
       1830 IF L$=F$(A,1)THEN 1870
       1840 IF A<FL THEN 1820
       1850 DISPLAY AT(22,1):L$;" NOT FOUND"
       1860 RETURN
       1870 GOSUB 920
       1880 F$(A,2)=P$
       1890 DISPLAY AT(22,1):"NAME?"
       1900 ACCEPT AT(22,7)VALIDATE(UALPHA," ")SIZE(8):F$(A,1)
       1910 RETURN